;;; blame-reveal.el --- Git blame visualization in fringe -*- lexical-binding: t; -*-

;; Copyright (C) 2024 Lucius Chen
;; Author: Lucius Chen
;; Version: 0.5
;; Package-Requires: ((emacs "27.1"))
;; Keywords: git, vc, convenience
;; URL: https://github.com/lucius-chen/blame-reveal

;;; Commentary:
;; Display git blame information in the fringe with adaptive color gradients.
;; Color intensity based on commit recency within an intelligent time window.
;; Show commit info above each block with sticky headers.
;; Performance optimized: lazy loading, smart caching, and viewport rendering.
;;
;; Key Features:
;; - Smart time-based commit selection with auto-calculation
;; - Gradient quality control (strict/auto/relaxed)
;; - Lazy loading for large files (configurable threshold)
;; - Incremental commit info loading with caching
;; - Recursive blame navigation with historical context
;; - Sticky headers for long commits
;; - Theme-aware color schemes with HSL customization
;; - Customizable display layouts (line/compact/full/none)
;;
;; Quick Start:
;;   M-x blame-reveal-mode
;;
;; Common Customizations:
;;   (setq blame-reveal-recent-days-limit 'auto)      ; Smart time window
;;   (setq blame-reveal-gradient-quality 'auto)       ; Balanced quality
;;   (setq blame-reveal-display-layout 'compact)      ; Header format
;;   (setq blame-reveal-color-scheme '(:hue 210 ...)) ; Color theme
;;
;; See full documentation: https://github.com/lucius-chen/blame-reveal

;;; Code:

(require 'vc-git)
(require 'cl-lib)

;; Load all modules
(require 'blame-reveal-core)
(require 'blame-reveal-state)
(require 'blame-reveal-git)
(require 'blame-reveal-overlay)
(require 'blame-reveal-color)
(require 'blame-reveal-header)
(require 'blame-reveal-ui)
(require 'blame-reveal-commands)

;;; Fringe Bitmap Definitions

(define-fringe-bitmap 'blame-reveal-full
  [#b11111111 #b11111111 #b11111111 #b11111111
   #b11111111 #b11111111 #b11111111 #b11111111
   #b11111111 #b11111111 #b11111111 #b11111111
   #b11111111 #b11111111 #b11111111 #b11111111]
  16 8 'center)

(define-fringe-bitmap 'blame-reveal-loading-bright
  [#b11111111 #b11111111 #b11111111 #b01111110
   #b01111110 #b00111100 #b00011000 #b00000000]
  8 8 'center)

(define-fringe-bitmap 'blame-reveal-loading-dim
  [#b11111111 #b11111111 #b11111111 #b01111110
   #b01111110 #b00111100 #b00011000 #b00000000]
  8 8 'center)

;;; Keymaps

(defvar blame-reveal-mode-map
  (let ((map (make-sparse-keymap)))
    (define-key map (kbd "q") #'blame-reveal-mode)
    (define-key map (kbd "c") #'blame-reveal-copy-commit-hash)
    (define-key map (kbd "d") #'blame-reveal-show-commit-diff)
    (define-key map (kbd "s") #'blame-reveal-show-commit-details)
    (define-key map (kbd "h") #'blame-reveal-show-file-history)
    (define-key map (kbd "l") #'blame-reveal-show-line-history)
    (define-key map (kbd "b") #'blame-reveal-blame-recursively)
    (define-key map (kbd "p") #'blame-reveal-blame-back)
    (define-key map (kbd "^") #'blame-reveal-blame-back)
    (define-key map (kbd "g") #'blame-reveal-blame-at-revision)
    (define-key map (kbd "r") #'blame-reveal-reset-to-head)
    map)
  "Keymap for blame-reveal-mode.")

(defvar blame-reveal--emulation-alist nil
  "Emulation mode map alist for blame-reveal.")

;;; Timer Variables

(defvar blame-reveal--theme-change-timer nil
  "Timer to debounce theme changes.")

(defvar blame-reveal--scroll-timer nil
  "Timer to debounce scroll updates.")

(defvar blame-reveal--scroll-render-delay 0.3
  "Internal delay before rendering overlays after scrolling stops.
This is an implementation detail and should not be customized by users.")

(defvar blame-reveal-loading-animation-speed 0.08
  "Loading animation update interval in seconds.")

(defvar blame-reveal-loading-animation-frames-per-step 4
  "Number of frames before advancing to next step.")

(defvar blame-reveal--global-loading-animation-timer nil
  "Global timer for loading animation across all buffers.")

;;; Customization Group

(defgroup blame-reveal nil
  "Show git blame in fringe with colors.

Quick start:
  M-x blame-reveal-mode

Common customizations:
  - `blame-reveal-color-scheme': Change color scheme
  - `blame-reveal-recent-days-limit': How many days of commits to highlight
  - `blame-reveal-header-format-function': Customize header display
  - `blame-reveal-use-magit': Use magit for commit details
  - `blame-reveal-lazy-load-threshold': Lazy loading threshold

See all options: M-x customize-group RET blame-reveal"
  :group 'vc
  :prefix "blame-reveal-")

;;; Header Customization

(defcustom blame-reveal-header-format-function
  #'blame-reveal-format-header-default
  "Function to format commit information for header display.

The function should accept three arguments:
  COMMIT-HASH: commit hash string
  COMMIT-INFO: commit info tuple (short-hash author date summary timestamp description)
  COLOR: base color for this commit (hex string)

And return a `blame-reveal-commit-display' struct with:
  :lines - List of strings (one per line)
  :faces - List of faces (one per line, can reference COLOR)
  :color - Base color for fringe indicators

The system automatically adapts for different contexts:
  - Normal header: shows full multi-line format
  - Inline header: auto-compresses to single line
  - Sticky header: reuses appropriate format

Example: Minimal style
  (setq blame-reveal-header-format-function
    (lambda (commit-hash info color)
      (pcase-let ((`(,hash ,_author ,_date ,msg ,_ ,_) info))
        (make-blame-reveal-commit-display
         :lines (list (format \"%s: %s\" hash msg))
         :faces (list `(:foreground ,color))
         :color color))))"
  :type 'function
  :group 'blame-reveal)

;;; Display Customization

(defcustom blame-reveal-style 'left-fringe
  "Which fringe to use for blame indicators."
  :type '(choice (const left-fringe)
                 (const right-fringe))
  :group 'blame-reveal)

(defcustom blame-reveal-header-position 'before-block
  "Position of commit message header relative to code block.
- `before-block': Show header above the first line of the block (default)
- `after-first-line': Show header after the first line of the block

When set to `after-first-line':
  - Header appears immediately after the first line of code
  - Maintains fixed distance from the code
  - For `compact' layout: shows as single line (msg · author · hash)
  - For `line' layout: shows only commit message
  - `full' layout is not supported in this mode (falls back to compact)"
  :type '(choice (const :tag "Before block (above first line)" before-block)
                 (const :tag "After first line (inline style)" after-first-line))
  :set (lambda (symbol value)
         (set-default symbol value)
         (when (and (boundp 'blame-reveal-mode) blame-reveal-mode)
           (dolist (buffer (buffer-list))
             (with-current-buffer buffer
               (when blame-reveal-mode
                 (when blame-reveal--header-overlay
                   (delete-overlay blame-reveal--header-overlay)
                   (setq blame-reveal--header-overlay nil))
                 (blame-reveal--update-header-impl))))))
  :group 'blame-reveal)

;;; Commit Selection Customization

(defcustom blame-reveal-recent-days-limit 'auto
  "Time limit for highlighting recent commits.

Using days (instead of commit count) ensures consistent behavior
across projects with different commit frequencies.

Values:
  'auto  - Automatically calculate based on commit density and
           gradient quality. Adapts to both normal and recursive
           blame modes. (Recommended)

  number - Fixed days (e.g., 30, 90, 180, 365)
           Shows commits within this many days from reference point.
           In recursive blame, reference point is the revision date.

  nil    - No limit, highlight all commits with relative coloring
           (like IntelliJ IDEA)

Examples:
  'auto - Smart calculation, good for most cases
  30    - Last month (for very active files)
  90    - Last quarter
  180   - Last half year
  365   - Last year
  nil   - All commits (relative age coloring)

The actual number of commits within this period varies by file activity:
- Active files: many commits in the time window
- Quiet files: few commits in the time window

This is intentional and provides consistent time-based context
regardless of commit frequency."
  :type '(choice (const :tag "Auto (smart calculation)" auto)
                 (integer :tag "Fixed days")
                 (const :tag "No limit (all commits)" nil))
  :group 'blame-reveal)

(defcustom blame-reveal-gradient-quality 'auto
  "Control trade-off between time coverage and color distinction.

This setting affects how 'auto mode calculates the days limit.
It controls the target number of commits and minimum color difference.

'strict  - Fewer commits (5-10), excellent distinction (3-5% color steps)
           Best for visual clarity, shorter time window.
           Recommended for files with many commits.

'auto    - Balanced (10-20 commits), good distinction (2-3% color steps)
           Good balance between clarity and historical context.
           Recommended for most cases.

'relaxed - More commits (15-30), acceptable distinction (1.5-2% color steps)
           More historical context, colors may be subtle.
           Recommended for files with few commits.

Only used when `blame-reveal-recent-days-limit' is 'auto.
For fixed days limit, this setting is ignored.

Color distinction is measured by the lightness/saturation step between
consecutive commits in the gradient. Smaller steps mean colors are
harder to distinguish visually."
  :type '(choice (const :tag "Strict (best distinction)" strict)
                 (const :tag "Auto (balanced)" auto)
                 (const :tag "Relaxed (more history)" relaxed))
  :group 'blame-reveal)

;;; Uncommitted Changes Customization

(defcustom blame-reveal-uncommitted-label "Uncommitted changes"
  "Label to show for uncommitted changes."
  :type 'string
  :group 'blame-reveal)

(defcustom blame-reveal-uncommitted-color nil
  "Color for uncommitted changes header and fringe.

If nil, uses automatic color based on theme:
  - Dark theme: #d9a066 (muted orange)
  - Light theme: #e6b380 (light muted orange)

These colors are designed to have similar visual prominence to
committed changes while being clearly distinguishable.

Set to a color string to use a fixed color.

Note: If you find the fringe too prominent, consider setting
`blame-reveal-show-uncommitted-fringe' to nil and using diff-hl
instead for showing uncommitted changes."
  :type '(choice (const :tag "Auto (theme-based muted orange)" nil)
                 (color :tag "Fixed color"))
  :group 'blame-reveal)

(defcustom blame-reveal-show-uncommitted-fringe nil
  "Whether to show fringe indicators for uncommitted changes.

When nil (default and recommended):
  - Only show header when cursor is on uncommitted lines
  - No fringe indicators for uncommitted changes
  - Works well with diff-hl or git-gutter for showing changes

When t:
  - Show fringe indicators for uncommitted changes
  - Only recommended if you don't use diff-hl/git-gutter
  - May cause visual redundancy with those tools

Tip: Use this package with diff-hl-mode for the best experience:
  - blame-reveal shows git blame for committed changes
  - diff-hl shows uncommitted changes in fringe"
  :type 'boolean
  :group 'blame-reveal)

;;; Color Customization

(defcustom blame-reveal-recent-commit-color nil
  "Color for recent commits (within top N and time limit).
If nil, uses automatic gradient based on commit rank.
Can be:
- nil: Auto gradient (continuous gradient based on rank)
- Color string: Fixed color like \"#6699cc\" for all recent commits
- Function: Takes timestamp, returns color for custom gradient logic"
  :type '(choice (const :tag "Auto (gradient based on rank)" nil)
                 (color :tag "Fixed color")
                 (function :tag "Function (timestamp -> color)"))
  :group 'blame-reveal)

(defcustom blame-reveal-old-commit-color nil
  "Color for old commits (not in top N or beyond time limit).
If nil, uses automatic gray color based on theme (dark theme: #4a4a4a, light theme: #d0d0d0).
Set to a color string like \"#888888\" to use a fixed color."
  :type '(choice (const :tag "Auto (theme-based gray)" nil)
                 (color :tag "Fixed color"))
  :group 'blame-reveal)

(defcustom blame-reveal-color-scheme
  '(:hue 210
         :dark-newest 0.70
         :dark-oldest 0.35
         :light-newest 0.45
         :light-oldest 0.75
         :saturation-min 0.25
         :saturation-max 0.60)
  "Color scheme for blame visualization.
All lightness values are between 0.0 and 1.0.

Dark theme: newest commits are brighter (higher lightness) to stand out
Light theme: newest commits are darker (lower lightness) to stand out

Example schemes:

  High contrast:
  '(:hue 210
    :dark-newest 0.75 :dark-oldest 0.30
    :light-newest 0.35 :light-oldest 0.85
    :saturation-min 0.35 :saturation-max 0.70)

  Green:
  '(:hue 120
    :dark-newest 0.70 :dark-oldest 0.35
    :light-newest 0.40 :light-oldest 0.75
    :saturation-min 0.25 :saturation-max 0.60)

  Purple:
  '(:hue 280
    :dark-newest 0.70 :dark-oldest 0.35
    :light-newest 0.45 :light-oldest 0.75
    :saturation-min 0.25 :saturation-max 0.60)

  Subtle:
  '(:hue 210
    :dark-newest 0.60 :dark-oldest 0.40
    :light-newest 0.55 :light-oldest 0.70
    :saturation-min 0.20 :saturation-max 0.45)"
  :type 'plist
  :set (lambda (symbol value)
         (set-default symbol value)
         (when (and (boundp 'blame-reveal-mode) blame-reveal-mode)
           (dolist (buffer (buffer-list))
             (with-current-buffer buffer
               (when blame-reveal-mode
                 (blame-reveal--recolor-and-render))))))
  :group 'blame-reveal)

(defcustom blame-reveal-margin-height 0.75
  "Font height for commit message in left margin.
1.0 means default size, 0.75 means 75% of default, 1.1 means 110%.
Left margin width varies proportionally based on this value."
  :type 'number
  :group 'blame-reveal)

(defcustom blame-reveal--margin-width 32
  "Left margin width for IDEA style commit message. Default is 32 char width.
还需要根据blame-reveal--ensure-fringe-face中的height的比例计算实际的left margin宽度.
如height是0.75，那么实际宽度是32*0.75"
  :type 'number
  :group 'blame-reveal)

(defcustom blame-reveal-margin-time-format "%Y/%m/%d"
  "Format for time strings in left margin."
  :type 'string
  :group 'blame-reveal)


;;; Performance Customization

(defcustom blame-reveal-render-margin 50
  "Number of lines to render above and below visible window."
  :type 'integer
  :group 'blame-reveal)

(defcustom blame-reveal-temp-overlay-delay 0.05
  "Delay in seconds before rendering temp overlays for old commits.
Lower values (e.g., 0.02) make overlays appear faster but may cause lag
when moving cursor quickly. Higher values (e.g., 0.1) reduce lag but
overlays appear with more delay."
  :type 'number
  :group 'blame-reveal)

(defcustom blame-reveal-lazy-load-threshold 3000
  "File size threshold (in lines) for lazy loading.
Files larger than this will use lazy loading (only blame visible region).
Smaller files will load completely for better recursive blame experience.

Recommended values:
- 3000-5000: Good balance for most cases
- 1000-2000: If you have slow git repository
- 10000+: If you have fast SSD and want full loading for larger files"
  :type 'integer
  :group 'blame-reveal)

(defcustom blame-reveal-async-blame 'auto
  "Whether to use async loading for git blame data.

When enabled, git blame commands run in background processes,
keeping Emacs responsive during loading and scrolling.

Values:
  'auto - Use async for large files (> `blame-reveal-lazy-load-threshold' lines),
          sync for small files (recommended)
  t     - Always use async loading
  nil   - Always use synchronous loading (may block UI for large files)

Async loading provides better UX for large files but requires Emacs 25.1+.
For small files, sync loading is actually faster due to less overhead."
  :type '(choice (const :tag "Auto (async for large files)" auto)
                 (const :tag "Always async" t)
                 (const :tag "Always sync" nil))
  :group 'blame-reveal)

;;; Integration Customization

(defcustom blame-reveal-use-magit 'auto
  "Whether to use magit for showing commit details.
- 'auto: Use magit if available, otherwise use built-in
- t: Always use magit (error if not available)
- nil: Always use built-in git commands"
  :type '(choice (const :tag "Auto (use magit if available)" auto)
                 (const :tag "Always use magit" t)
                 (const :tag "Always use built-in" nil))
  :group 'blame-reveal)

;;; Mode Line Functions

(defun blame-reveal--mode-line-revision ()
  "Return mode line string showing current revision."
  (when blame-reveal--revision-display
    (format " @%s"
            (propertize blame-reveal--revision-display
                        'face 'font-lock-constant-face))))

(defun blame-reveal--setup-mode-line ()
  "Add revision info to mode line."
  (unless (member '(:eval (blame-reveal--mode-line-revision)) global-mode-string)
    (setq global-mode-string
          (append global-mode-string
                  '((:eval (blame-reveal--mode-line-revision)))))))

(defun blame-reveal--cleanup-mode-line ()
  "Remove revision info from mode line."
  (setq global-mode-string
        (delete '(:eval (blame-reveal--mode-line-revision)) global-mode-string)))

;;; Minor Mode Definition

;;;###autoload
(define-minor-mode blame-reveal-mode
  "Toggle git blame fringe display."
  :lighter " BlameReveal"
  :group 'blame-reveal
  (if blame-reveal-mode
      (progn
        (let ((file (buffer-file-name)))
          (if (not (and file (vc-git-registered file)))
              (progn
                (message "Cannot enable blame-reveal-mode: not a git-tracked file")
                (setq blame-reveal-mode nil))

            (setq blame-reveal--emulation-alist
                  `((blame-reveal-mode . ,blame-reveal-mode-map)))
            (add-to-list 'emulation-mode-map-alists
                         'blame-reveal--emulation-alist)

            ;; Initialize unified overlay management
            (blame-reveal--init-overlay-registry)
            (blame-reveal--init-color-strategy)

            ;; Initialize state
            (setq blame-reveal--auto-days-cache nil)
            (setq blame-reveal--blame-data-range nil)
            (setq blame-reveal--all-commits-loaded nil)

            ;; Load blame data
            (blame-reveal--load-blame-data)

            ;; 因为在blame-reveal--ensure-fringe-face margin的字体height是0.75，所以left-margin-width只需要原来的0.75即可
            (setq left-margin-width (ceiling (* blame-reveal-margin-height blame-reveal--margin-width)))
            (message "blame-reveal-mode left-margin-width: %d" left-margin-width)
            (set-window-buffer (selected-window) (current-buffer))

            (add-hook 'after-save-hook #'blame-reveal--full-update nil t)
            (add-hook 'window-scroll-functions #'blame-reveal--scroll-handler nil t)
            (add-hook 'post-command-hook #'blame-reveal--update-header nil t)
            (add-hook 'window-configuration-change-hook #'blame-reveal--render-visible-region nil t)

            (blame-reveal--setup-mode-line)
            (setq blame-reveal--blame-stack nil)
            (setq blame-reveal--current-revision nil)
            (setq blame-reveal--revision-display nil)

            (blame-reveal--setup-theme-advice))))

    ;; Cleanup when disabling mode
    (setq left-margin-width 0)
    (set-window-buffer (selected-window) (current-buffer))

    (setq emulation-mode-map-alists
          (delq 'blame-reveal--emulation-alist
                emulation-mode-map-alists))

    (blame-reveal--stop-loading-animation)
    (blame-reveal--state-cancel "mode disabled")

    (when blame-reveal--temp-overlay-timer
      (cancel-timer blame-reveal--temp-overlay-timer)
      (setq blame-reveal--temp-overlay-timer nil))

    (when blame-reveal--header-update-timer
      (cancel-timer blame-reveal--header-update-timer)
      (setq blame-reveal--header-update-timer nil))

    ;; Use unified cleanup
    (blame-reveal--clear-all-overlays)

    ;; Clear legacy header overlay
    (when blame-reveal--header-overlay
      (delete-overlay blame-reveal--header-overlay)
      (setq blame-reveal--header-overlay nil))

    (blame-reveal--clear-sticky-header)

    (remove-hook 'after-save-hook #'blame-reveal--full-update t)
    (remove-hook 'window-scroll-functions #'blame-reveal--scroll-handler t)
    (remove-hook 'post-command-hook #'blame-reveal--update-header t)
    (remove-hook 'window-configuration-change-hook #'blame-reveal--render-visible-region t)

    (when blame-reveal--scroll-timer
      (cancel-timer blame-reveal--scroll-timer)
      (setq blame-reveal--scroll-timer nil))

    (setq blame-reveal--state-status 'idle
          blame-reveal--state-operation nil
          blame-reveal--state-mode nil
          blame-reveal--state-metadata nil
          blame-reveal--state-process nil
          blame-reveal--state-buffer nil)

    (setq blame-reveal--auto-days-cache nil)
    (setq blame-reveal--blame-data-range nil)
    (setq blame-reveal--all-commits-loaded nil)

    (unless (cl-some (lambda (buf)
                       (with-current-buffer buf
                         (and (not (eq buf (current-buffer)))
                              blame-reveal-mode)))
                     (buffer-list))

      (blame-reveal--cleanup-mode-line)
      (setq blame-reveal--blame-stack nil)
      (setq blame-reveal--current-revision nil)
      (setq blame-reveal--revision-display nil)

      (blame-reveal--remove-theme-advice))))

(provide 'blame-reveal)
;;; blame-reveal.el ends here
